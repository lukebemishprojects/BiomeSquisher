pluginManagement {
	repositories {
        gradlePluginPortal()
		maven {
			name = 'Fabric'
			url = 'https://maven.fabricmc.net/'
		}
        maven {
            name = 'NeoForged'
            url = 'https://maven.neoforged.net/releases'
        }
        maven {
            name = "Luke's Maven"
            url = 'https://maven.lukebemish.dev/releases/'
        }
        maven {
            name = 'Architectury'
            url "https://maven.architectury.dev/"
        }
	}
}

plugins {
    id 'com.gradle.enterprise' version '3.15'
}

gradleEnterprise {
    if (System.getenv('CI') != null) {
        buildScan {
            publishAlways()
            termsOfServiceUrl = "https://gradle.com/terms-of-service"
            termsOfServiceAgree = "yes"
        }
    }
}

rootProject.name = "biomesquisher"

include 'common', 'fabric', 'neoforge'

buildCache {
    // Build caching is currently rather useless for neoforge projects, due to the nature of NeoGradle 7
    if (providers.gradleProperty('buildCacheUrl').orNull) {
        remote(HttpBuildCache) {
            url = providers.gradleProperty('buildCacheUrl').orNull
            credentials {
                username = providers.gradleProperty('buildCacheUser').orNull
                password = providers.gradleProperty('buildCachePassword').orNull
            }
            if (providers.gradleProperty('buildCachePush').orNull) {
                push = true
            } else {
                push = false
            }
        }
    } else if (System.getenv('BUILD_CACHE_URL')) {
        remote(HttpBuildCache) {
            url = System.getenv('BUILD_CACHE_URL')
            credentials {
                username = System.getenv('BUILD_CACHE_USER')
                password = System.getenv('BUILD_CACHE_PASSWORD')
            }
            if (System.getenv('BUILD_CACHE_PUSH') || System.getenv('CI')) {
                push = true
            } else {
                push = false
            }
        }
    }
}
